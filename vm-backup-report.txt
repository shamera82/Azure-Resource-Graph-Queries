Resources | where type in~ ('microsoft.compute/virtualmachines','microsoft.classiccompute/virtualmachines')
| extend armResourceId = id
| extend resourceId=tolower(armResourceId) | join kind = leftouter ( RecoveryServicesResources | where id contains "IaasVMContainer"
| extend lastBackup_status = properties.lastBackupStatus
| extend protection_status = properties.protectionStatus
| extend currentProtectionState = properties.currentProtectionState
| extend policy_name = properties.policyInfo.name
| extend lastbackup = properties.lastBackupTime
| extend lastRecoveryPoint = properties.lastRecoveryPoint
| project resourceId = tolower(tostring(properties.sourceResourceId)), backupItemid = id, policy_name, currentProtectionState, lastBackup_status, protection_status, lastbackup, lastRecoveryPoint, isBackedUp = isnotempty(id) ) on resourceId
| extend sku_2 = properties.storageProfile.imageReference.sku
| extend publisher = properties.storageProfile.imageReference.publisher
| extend offer = properties.storageProfile.imageReference.offer
| extend osType = properties.storageProfile.osDisk.osType
| extend osVersion = properties.extended.instanceView.osVersion
| extend osName = properties.extended.instanceView.osName
| extend PowerStatus = properties.extended.instanceView.powerState.displayStatus
| extend env_tag = tags.Environment
| extend app_tag = tags.App
| extend description_tag = tags.Description
| extend AssetName_tag = tags.AssetName
| project tenantId,location, env_tag, app_tag, description_tag,AssetName_tag, resourceId,name, type, subscriptionId, resourceGroup, isBackedUp, currentProtectionState, backupItemid, sku_2, publisher, offer, osType, osVersion, osName, PowerStatus,policy_name, lastBackup_status, protection_status, lastbackup, lastRecoveryPoint
